plugins {
  id "com.github.maiflai.scalatest" version "0.6-5-g9065d91"
}
apply plugin: 'java'
apply plugin: 'scala'

task wrapper(type: Wrapper) {
  gradleVersion = '2.10'
}

sourceSets {
  main.java.exclude 'usbrubberducky/encoder/Encoder_GUI.java'
  neo {
    resources.srcDir 'src/main/resources'
  }
}

repositories { mavenCentral() }

configurations {
  testRuntime { extendsFrom neoRuntime }
}

dependencies {
  neoCompile(
    'org.scala-lang:scala-library:2.11+',
    'org.scala-lang:scala-parser-combinators:+',
  )
  testCompile(
    sourceSets.neo.output,
    'org.scalatest:scalatest_2.11:2.+',
    'org.scalamock:scalamock-core_2.11:3.+',
    'org.scalamock:scalamock-scalatest-support_2.11:3.+',
  )
  testRuntime 'org.pegdown:pegdown:1.+'
}

// Use fast, incremental scala compiler
tasks.withType(ScalaCompile) {
  scalaCompileOptions.useAnt = false
  scalaCompileOptions.additionalParameters = ['-feature']
}

jar {
  archiveName = 'encoder.jar'
  manifest {
    from 'src/main/resources/META-INF/MANIFEST.MF'
  }
}

task neoJar(type: Jar, dependsOn: neoClasses) {
  from sourceSets.neo.output

  configurations.neoRuntime.files.each {
    from zipTree(it)
  }

  archiveName = 'new-encoder.jar'
  manifest.from 'src/neo/resources/META-INF/MANIFEST.MF'
}

apply from: 'test.gradle'
